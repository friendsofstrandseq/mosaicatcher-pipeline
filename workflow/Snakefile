configfile_location = "config/config.yaml"


configfile: configfile_location


report: "report/workflow.rst"


docker_container = "docker://weber8thomas/mosaicatcher-pipeline:{version}".format(
    version=str(config["version"])
)


containerized: docker_container


if config["ashleys_pipeline"] is True:

    module ashleys_qc:
        snakefile:
            "../../ashleys-qc-pipeline/workflow/Snakefile"
            # github(
            #     "friendsofstrandseq/ashleys-qc-pipeline",
            #     path="workflow/Snakefile",
            #     tag=str(config["ashleys_pipeline_version"]),
            # )
        config:
            config

    use rule * from ashleys_qc as ashleys_*

    if config["ashleys_pipeline_only"] is True:

        localrules:
            ashleys_genecore_symlink,
            ashleys_symlink_selected_bam,

    else:

        localrules:
            ashleys_genecore_symlink,
            ashleys_symlink_selected_bam,
            symlink_selected_bam,

else:

    localrules:
        symlink_selected_bam,


include: "rules/common.smk"


include: "rules/aggregate_fct.smk"


include: "rules/external_data.smk"


include: "rules/setup.smk"


include: "rules/utils.smk"


include: "rules/count.smk"


include: "rules/segmentation.smk"


include: "rules/plots.smk"


include: "rules/regenotyping.smk"


include: "rules/strandphaser.smk"


include: "rules/haplotagging.smk"


include: "rules/mosaiclassifier.smk"


include: "rules/postprocessing.smk"


include: "rules/ploidy.smk"


include: "rules/stats.smk"


include: "rules/arbigent_rules.smk"


include: "rules/arbigent.smk"


include: "rules/scNOVA.smk"


include: "rules/gc.smk"


include: "rules/debug.smk"


if config["list_commands"] is False:
    if config["ashleys_pipeline"] is True:
        if config["ashleys_pipeline_only"] is False:

            rule all:
                input:
                    rules.ashleys_all.input,
                    get_final_output(),
                default_target: True

        else:

            rule all:
                input:
                    rules.ashleys_all.input,
                default_target: True

    else:

        rule all:
            input:
                get_final_output(),

    if config["email"]:

        onsuccess:
            onsuccess_fct(log)

        onerror:
            onerror_fct(log)
